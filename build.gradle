import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
	ext {
		kotlinVersion = '1.2.41'
		springBootVersion = '1.5.14.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath("gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:5.1.4")
	}
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.flywaydb.flyway'

group = 'io.pivotal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-actuator")
	compile("org.springframework.boot:spring-boot-starter-jdbc")
	compile("org.springframework.boot:spring-boot-starter-security")
	compile("mysql:mysql-connector-java:8.0.11")
	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
	compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.1")
	compile('com.fasterxml.jackson.module:jackson-module-kotlin')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
		"WELCOME_MESSAGE": "hello",
		"SPRING_DATASOURCE_URL": developmentDbUrl,
		"MANAGEMENT_SECURITY_ENABLED": false,
		"HTTPS_DISABLED": true
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
		"WELCOME_MESSAGE": "Hello from test",
		"SPRING_DATASOURCE_URL": testDbUrl,
		"MANAGEMENT_SECURITY_ENABLED": false,
		"HTTPS_DISABLED": true
])

flyway {
	url = developmentDbUrl
	user = "tracker"
	password = ""
	locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
	url = testDbUrl
}

springBoot {
	buildInfo()
}